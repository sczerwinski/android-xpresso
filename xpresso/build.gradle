/*
 * Copyright 2020 Slawomir Czerwinski
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

apply from: '../common-android.gradle'

dependencies {
    api project(path: ':xpresso-core')
    api project(path: ':xpresso-recyclerview')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

def withSigning = hasProperty('signing.keyId')
def isSnapshot = libVersion.endsWith('SNAPSHOT')

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = "$libGroupId"
                artifactId = "${project.name}"
                version = "$libVersion"

                pom {
                    name = libName
                    description = libDescription
                    url = libUrl
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'sczerwinski'
                            name = 'Slawomir Czerwinski'
                            email = 'slawomir@czerwinski.it'
                        }
                    }
                    scm {
                        connection = scmConnection
                        developerConnection = scmConnection
                        url = scmUrl
                    }
                }
            }
        }
        repositories {
            maven {
                if (System.getenv('SONATYPE_USERNAME') != null) {
                    logger.quiet("Publishing to Sonatype")
                    url = isSnapshot ? ossrhSnapshotRepo : ossrhRepo
                    credentials {
                        username = System.getenv('SONATYPE_USERNAME')
                        password = System.getenv('SONATYPE_PASSWORD')
                    }
                } else if (project.hasProperty('ossrhUsername')) {
                    logger.quiet("Publishing to Sonatype")
                    url = isSnapshot ? ossrhSnapshotRepo : ossrhRepo
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                } else {
                    logger.quiet("Publishing to local repository")
                    url = "${buildDir}/repos"
                }
            }
        }
    }

    if (withSigning) {
        signing {
            sign publishing.publications.release
        }
    }
}
